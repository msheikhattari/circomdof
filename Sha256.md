```
template Sha256(nBits) {
    signal input in[nBits]; \\s=n
    signal output out[256]; \\s=n+256

    var i;
    var k;
    var nBlocks;
    var bitsLastBlock;


    nBlocks = ((nBits + 64)\512)+1;

    signal paddedIn[nBlocks*512]; \\s=n+256 + (n+64 + 512 - (n+64)%512)

    for (k=0; k<nBits; k++) {
        paddedIn[k] <== in[k];
    }
    \\c=n
    
    paddedIn[nBits] <== 1;
    \\c=n+1

    for (k=nBits+1; k<nBlocks*512-64; k++) {
        paddedIn[k] <== 0;
    }
    \\c=n+1 + (511 - (n+64) % 512) = n + 512 - (n+64)%512

    for (k = 0; k< 64; k++) {
        paddedIn[nBlocks*512 - k -1] <== (nBits >> k)&1;
    }
    \\ c = n + 576 - (n+64)%512

    component ha0 = H(0); \\component
    component hb0 = H(1); \\component
    component hc0 = H(2); \\component
    component hd0 = H(3); \\component
    component he0 = H(4); \\component
    component hf0 = H(5); \\component
    component hg0 = H(6); \\component
    component hh0 = H(7); \\component

    component sha256compression[nBlocks]; \\component

    for (i=0; i<nBlocks; i++) {

        sha256compression[i] = Sha256compression() ; \\component

        if (i==0) {
            for (k=0; k<32; k++ ) {
                sha256compression[i].hin[0*32+k] <== ha0.out[k];
                sha256compression[i].hin[1*32+k] <== hb0.out[k];
                sha256compression[i].hin[2*32+k] <== hc0.out[k];
                sha256compression[i].hin[3*32+k] <== hd0.out[k];
                sha256compression[i].hin[4*32+k] <== he0.out[k];
                sha256compression[i].hin[5*32+k] <== hf0.out[k];
                sha256compression[i].hin[6*32+k] <== hg0.out[k];
                sha256compression[i].hin[7*32+k] <== hh0.out[k];
            }
            \\ c=8*32 + n + 576 - (n+64)%512 = n + 832 - (n+64)%512
        } else {
            for (k=0; k<32; k++ ) {
                sha256compression[i].hin[32*0+k] <== sha256compression[i-1].out[32*0+31-k];
                sha256compression[i].hin[32*1+k] <== sha256compression[i-1].out[32*1+31-k];
                sha256compression[i].hin[32*2+k] <== sha256compression[i-1].out[32*2+31-k];
                sha256compression[i].hin[32*3+k] <== sha256compression[i-1].out[32*3+31-k];
                sha256compression[i].hin[32*4+k] <== sha256compression[i-1].out[32*4+31-k];
                sha256compression[i].hin[32*5+k] <== sha256compression[i-1].out[32*5+31-k];
                sha256compression[i].hin[32*6+k] <== sha256compression[i-1].out[32*6+31-k];
                sha256compression[i].hin[32*7+k] <== sha256compression[i-1].out[32*7+31-k];
            }
            \\c = 8*32*(((n + 64)\512)+1) + n + 832 - (n+64)%512
        }

        for (k=0; k<512; k++) {
            sha256compression[i].inp[k] <== paddedIn[i*512+k];
        }
        \\c = 8*32*(((n + 64)\512)+1) + n + 1344 - (n+64)%512
    }

    for (k=0; k<256; k++) {
        out[k] <== sha256compression[nBlocks-1].out[k];
    }
    \\c = 256*(((n + 64)\512)+1) + n + 1600 - (n+64)%512

}
```
2n+832 - ((n+64)%512) signals,  256((n + 64)\512) + n + 1856 - (n+64)%512 constraints. The dof of [[H]] is 0 (called 8 times). The dof of [[Sha256compression]] is 456 (called ((n + 64)\512)+1 times). In total: 2n+832 - ((n+64)%512) - (256((n + 64)\512) + n + 1856 - (n+64)%512) + 456(((n + 64)\512)+1) = n + 200((n + 64)\512) - 568.

`dof = n + 200((n + 64)\512) - 568`